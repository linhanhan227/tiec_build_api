包名 结绳.安卓

/*
安卓提示框类
*/
@禁止创建对象
@指代类("android.widget.Toast")
类 安卓提示框
	@静态
	常量 长时 : 整数 = 1
	@静态
	常量 短时 : 整数 = 0

	@静态
	方法 新建提示框(环境 : 安卓环境) : 安卓提示框
		code return android.widget.Toast.makeText(#环境,"",0);
	结束 方法

	@静态
	方法 弹出提示(环境 : 安卓环境, 内容 : 文本, 长时显示 : 逻辑型 = 假)
		code android.widget.Toast.makeText(#环境, #内容, #长时显示 ? 1 : 0).show();
	结束 方法

	/*
	获取提示框显示时长
	*/
	属性读 显示时长() : 整数
		code return #this.getDuration();
	结束 属性

	/*
	设置提示框显示时长
	*/
	属性写 显示时长(时长 : 整数 = 安卓提示框.短时)
		code #this.setDuration(#时长);
	结束 属性

	/*
	获取提示框自定义布局
	*/
	属性读 布局() : 可视化组件
		code return (#cls<可视化组件>) #this.getView().getTag();
	结束 属性

	/*
	设置提示框自定义布局
	*/
	属性写 布局(组件 : 可视化组件)
		code #this.setView(#组件.getView());
	结束 属性

	/*
	设置提示框显示内容
	*/
	属性写 内容(内容 : 文本)
		code #this.setText(#内容);
	结束 属性

	/*
	获取提示框横向边距
	*/
	属性读 横向边距() : 整数
		code return (int) #this.getHorizontalMargin();
	结束 属性

	/*
	获取提示框纵向边距
	*/
	属性读 纵向边距() : 整数
		code return (int) #this.getVerticalMargin();
	结束 属性

	/*
	获取提示框横向偏移
	*/
	属性读 横向偏移() : 整数
		code return #this.getXOffset();
	结束 属性

	/*
	获取提示框纵向偏移
	*/
	属性读 纵向偏移() : 整数
		code return #this.getYOffset();
	结束 属性

	/*
	获取提示框对齐方式
	*/
	属性读 对齐方式() : 整数
		code return #this.getGravity();
	结束 属性

	/*
	将提示框显示出来
	*/
	方法 显示()
		code #this.show();
	结束 方法

	/*
	将提示框取消
	*/
	方法 取消()
		code #this.cancel();
	结束 方法

	/*
	设置提示框显示时的边距
	*/
	方法 置边距(横向边距 : 整数, 纵向边距 : 整数)
		code #this.setMargin(#横向边距, #纵向边距);
	结束 方法

	/*
	设置提示框的对齐方式，如(靠上对齐)
	*/
	方法 置对齐方式(对齐方式 : 整数, 横向偏移 : 整数, 纵向偏移 : 整数)
		code #this.setGravity(#对齐方式, #横向偏移, #纵向偏移);
	结束 方法
结束 类

/*
提示框组件
*/
类 提示框 : 窗口组件
	@静态
	常量 长时 : 整数 = 1
	@静态
	常量 短时 : 整数 = 0

	@隐藏
	变量 提示框对象 : 安卓提示框?

	@code
	public #cls<提示框>(android.content.Context context) {
		super(context);
		#提示框对象 = android.widget.Toast.makeText(context, "", 0);
	}
	@end

	/*
	获取提示框显示时长
	*/
	属性读 显示时长() : 整数
		返回 提示框对象.显示时长
	结束 属性

	/*
	设置提示框显示时长
	*/
	属性写 显示时长(时长 : 整数 = 提示框.短时)
		提示框对象.显示时长 = 时长
	结束 属性

	/*
	获取提示框自定义布局
	*/
	属性读 布局() : 可视化组件
		返回 提示框对象.布局
	结束 属性

	/*
	设置提示框自定义布局
	*/
	属性写 布局(组件 : 可视化组件)
		提示框对象.布局 = 组件
	结束 属性

	/*
	设置提示框显示内容
	*/
	属性写 内容(内容 : 文本)
		提示框对象.内容 = 内容
	结束 属性

	/*
	获取提示框横向边距
	*/
	属性读 横向边距() : 整数
		返回 提示框对象.横向边距
	结束 属性

	/*
	获取提示框纵向边距
	*/
	属性读 纵向边距() : 整数
		返回 提示框对象.纵向边距
	结束 属性

	/*
	获取提示框横向偏移
	*/
	属性读 横向偏移() : 整数
		返回 提示框对象.横向偏移
	结束 属性

	/*
	获取提示框纵向偏移
	*/
	属性读 纵向偏移() : 整数
		返回 提示框对象.纵向偏移
	结束 属性

	/*
	获取提示框对齐方式
	*/
	属性读 对齐方式() : 整数
		返回 提示框对象.对齐方式
	结束 属性

	/*
	将提示框显示出来
	*/
	方法 显示()
		提示框对象.显示()
	结束 方法

	/*
	将提示框取消
	*/
	方法 取消()
		提示框对象.取消()
	结束 方法

	/*
	设置提示框显示时的边距
	*/
	方法 置边距(横向边距 : 整数, 纵向边距 : 整数)
		提示框对象.置边距(横向边距, 纵向边距)
	结束 方法

	/*
	设置提示框的对齐方式，如(靠上对齐)
	*/
	方法 置对齐方式(对齐方式 : 整数, 横向偏移 : 整数, 纵向偏移 : 整数)
		提示框对象.置对齐方式(对齐方式, 横向偏移, 纵向偏移)
	结束 方法

	@静态
	方法 弹出提示(环境 : 安卓环境, 内容 : 文本, 长时显示 : 逻辑型 = 假)
		code android.widget.Toast.makeText(#环境, #内容, #长时显示 ? 1 : 0).show();
	结束 方法
结束 类